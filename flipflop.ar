reactive FlipFlop on, off -> flipflop {
  input:
    on  : Bool;
    off : Bool;

  output:
    flipflop : Bool;

  state:
    flipflop : Bool = if   on and off then undefined
                      elif on         then true
                      elif off        then false
                                      else init_state;

  static:
    init_state = false;

  rules:
    flipflop = true   if on;
    flipflop = false  if off;
}


reactive FlipFlopClient -> flipflop_client {
  input:
    action : <on, off, same, none>?;

  output:
    flipflop_client : Bool;
    // flipflop_state_bad : Bool;

  state:
    on  : Bool = false;
    off : Bool = false;

  rules:
    on = true  if action == :on;
    on = false if action == :off;

    off = true  if action == :off;
    off = false if action == :on;


    flipflop = FlipFlop(on=on, off=off);

    flipflop_client = flipflop;

    // flipflop_state_bad = flipflop.flipflop;
}


reactive FlipFlopClientPositional -> flipflop {
  input:
    action : <on, off, same, none>?;

  output:
    flipflop : Bool;

  state:
    on  : Bool = false;
    off : Bool = false;

  rules:
    on = true  if action == :on;
    on = false if action == :off;

    off = true  if action == :off;
    off = false if action == :on;

    flipflop = FlipFlop(on, off);
    // flipflop = flipflop(action? and action == :on, action? and action == :off);

    flipflop_client_positional = flipflop;
}


(<on, off, same, none>, Bool)* flipflop_test_data_1 = (
  (:none,   false),
  (:same,   false),
  (:off,    false),
  (:on,     true),
  (:none,   true),
  (:on,     true),
  (:same,   true),
  (:off,    false)
);


Bool RunBasicFlipFlopTest((<on, off, same, none>, Bool)* test_data) {
  auto fc  : FlipFlopClient;
  auto fcp : FlipFlopClientPositional;

  auto apply fc done;
  assert done;
  auto apply fcp done;
  assert done;
  if fc != false or fcp != false:
    Print("ERROR: INITIAL UPDATE FAILED SOMEHOW!\n");
    print fc;
    print fcp;
    return false;
  ;

  for a, er <- test_data:
    auto fc.action = a;
    auto fcp.action = a;
    auto apply fc done;
    assert done;
    auto apply fcp done;
    assert done;
    if fc != er or fcp != er:
      Print("ERROR! ERROR! ERROR!\n");
      print fc;
      print fcp;
      return false;
    // else
    //   Print("OK\n");
    ;
  ;

  return true;
}


Bool RunAllFlipFlopTests() {
  ok = RunBasicFlipFlopTest(flipflop_test_data_1);
  return ok;
}
