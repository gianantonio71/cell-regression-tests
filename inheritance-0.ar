reactive base
{
  input:
    in_a : Ta;
    in_b : Tb;

  output:
    out_a : Ta;
    out_b : Tb;

  rules:
    out_a = if in_a == :a0 then :a9 else in_a;
    out_b = if in_b == :b0 then :b9 else in_b;
}


reactive derived : base
{
  rules:
    out_a = if in_a == :a1 then :a8 else in_a;
}


reactive twice_derived : derived
{
  rules:
    out_b = if in_b == :b1 then :b8 else in_b;
}


reactive input_overriding : base
{
  input:
   new_in_a : Ta;
   new_in_b : Tb;

  rules:
    in_a = if new_in_a == :a2 then :a0 else new_in_a;
    in_b = if new_in_b == :b2 then :b0 else new_in_b;
}


(Ta, Tb, Ta, Tb, Ta, Tb, Ta, Tb, Ta, Tb)* basic_inheritance_test_data = (
  (:a0, :b0, :a9, :b9, :a0, :b9, :a0, :b0, :a9, :b9),
  (:a1, :b1, :a1, :b1, :a8, :b1, :a8, :b8, :a1, :b1),
  (:a2, :b2, :a2, :b2, :a2, :b2, :a2, :b2, :a9, :b9)
);


Bool RunAllBasicInheritanceTests()
{
  auto b : base;
  auto d : derived;
  auto t : twice_derived;
  auto i : input_overriding;

  for in_a, in_b, eba, ebb, eda, edb, eta, etb, eia, eib <- basic_inheritance_test_data:
    auto b.in_a = in_a;
    auto b.in_b = in_b;
    auto d.in_a = in_a;
    auto d.in_b = in_b;
    auto t.in_a = in_a;
    auto t.in_b = in_b;
    auto i.new_in_a = in_a;
    auto i.new_in_b = in_b;

    auto apply b;
    auto apply d;
    auto apply t;
    auto apply i;

    if b.out_a != eba or b.out_b != ebb:
      Print("ERROR: base\n");
      print (eba, ebb);
      print b.out_a;
      print b.out_b;
      return false;
    ;

    if d.out_a != eda or d.out_b != edb:
      Print("ERROR: derived\n");
      print (eda, edb);
      print b.out_a;
      print b.out_b;
      return false;
    ;

    if t.out_a != eta or t.out_b != etb:
      Print("ERROR: twice_derived\n");
      print (eta, etb);
      print b.out_a;
      print b.out_b;
      return false;
    ;

    if i.out_a != eia or i.out_b != eib:
      Print("ERROR: input_overriding\n");
      print (eia, eib);
      print b.out_a;
      print b.out_b;
      return false;
    ;

    // Print("OK\n");
  ;

  return true;
}
